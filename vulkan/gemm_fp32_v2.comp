#version 450 core
#extension GL_EXT_scalar_block_layout : enable
#extension GL_EXT_control_flow_attributes : enable
#extension GL_EXT_debug_printf : enable
// #extension GL_EXT_spirv_intrinsics : enable

layout(binding=0) buffer InputA { float data[]; } A;
layout(binding=1) buffer InputB { float data[]; } B;
layout(binding=2) buffer Output { float data[]; } C;


layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;
layout(local_size_x_id = 0, local_size_y_id = 1, local_size_z_id = 2) in; 

layout(push_constant) uniform Parameters {
   uint M;
   uint N;
   uint K;
} params;

// v2: 对应 gemm_mobile.cl 中的 v2，一个线程处理4*4个元素，提高计算访存比
void main() {
        
    const int STEP = 4;

    const uint lda = params.K;
    const uint ldb = params.N;
    const uint ldc = params.N;
    
    uvec2 lid = gl_LocalInvocationID.xy;
    uint gid_sx = (gl_WorkGroupID.x * gl_WorkGroupSize.x + lid.x) * STEP;
    uint gid_sy = (gl_WorkGroupID.y * gl_WorkGroupSize.y + lid.y) * STEP;

    if (gid_sx >= params.N || gid_sy >= params.M)
        return;

    float acc[STEP][STEP];
    for (int si = 0; si < STEP; si++) {
        for (int sj = 0; sj < STEP; sj++) {
            acc[si][sj] = 0; 
        }
    }
    
    for (int k = 0; k < params.K; k++) {
        for (int si = 0; si < STEP; si++) {
            for (int sj = 0; sj < STEP; sj++) {
                acc[si][sj] += A.data[(gid_sy+si) * lda + k] * B.data[k * ldb + gid_sx + sj];
            }
        }
    }
    for (int si=0; si<STEP; si++) {
        for (int sj=0; sj<STEP; sj++) {
            C.data[(gid_sy+si) * ldc + gid_sx+sj] += acc[si][sj];
        }
    }
}
